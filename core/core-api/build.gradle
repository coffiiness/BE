bootJar.enabled = true
jar.enabled = false

dependencies {
    // 도메인 모듈
    implementation project(':core:domain-user')
    implementation project(':core:domain-vote')
    implementation project(':core:core-enum')

    // 지원 모듈
    implementation project(':support:monitoring')
    implementation project(':support:logging')
    implementation project(':support:security')
    implementation project(':support:event')
    implementation project(':support:error')

    // 스토리지
    implementation project(':storage:db-core')

    // 외부 클라이언트 (MSA)
    implementation project(':clients:client-example')

    // 테스트 - REST Docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
    testImplementation 'io.rest-assured:spring-mock-mvc'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
}

def jacocoTargetProjects = rootProject.subprojects.findAll { subproject ->
    !['tests', 'support'].any { excluded -> subproject.path.contains(excluded) }
}

tasks.register('jacocoMergedReport', JacocoReport) {
    group = 'verification'
    description = 'Generates an aggregate JaCoCo report from all subprojects'

    dependsOn jacocoTargetProjects.collect { it.tasks.named('test') }
    dependsOn jacocoTargetProjects.collect { it.tasks.named('jacocoTestReport') }

    additionalSourceDirs.from(files(jacocoTargetProjects*.sourceSets.main*.allSource.srcDirs))
    sourceDirectories.from(files(jacocoTargetProjects*.sourceSets.main*.allSource.srcDirs))
    classDirectories.from(files(jacocoTargetProjects*.sourceSets.main*.output))
    executionData.from(fileTree(rootProject.projectDir).include('**/build/jacoco/test.exec'))

    reports {
        xml.required = true
        html.required = true
        xml.outputLocation = layout.buildDirectory.file("reports/jacoco/jacocoMergedReport/jacocoMergedReport.xml")
        html.outputLocation = layout.buildDirectory.dir("reports/jacoco/jacocoMergedReport/html")
    }
}