plugins {
    id 'java-library'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'io.spring.javaformat' apply(false)
    id 'org.asciidoctor.jvm.convert' apply(false)
    id 'org.sonarqube' version "${sonarqubeVersion}"
    id 'jacoco'
}

apply from: 'lint.gradle'

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"
    sourceCompatibility = project.javaVersion

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "${jacocoVersion}"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    bootJar.enabled = false
    jar.enabled = true

    test {
        useJUnitPlatform {
            excludeTags('develop', 'restdocs')
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.10
                }
            }
        }
    }

    check.dependsOn jacocoTestCoverageVerification

    tasks.register('restDocsTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('restdocs')
        }
    }

    tasks.register('developTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('develop')
        }
    }

    tasks.named('asciidoctor') {
        dependsOn restDocsTest
    }
}

sonar {
    properties {
        property 'sonar.projectKey', 'coffiiness_BE'
        property 'sonar.organization', 'coffiiness'
        property 'sonar.host.url', 'https://sonarcloud.io'

        property 'sonar.coverage.jacoco.xmlReportPaths',
                "${rootProject.projectDir}/core/core-api/build/reports/jacoco/jacocoMergedReport/jacocoMergedReport.xml"

        property 'sonar.exclusions', [
                '**/config/**',
                '**/entity/**',
                '**/*Application.java'
        ].join(',')

        property 'sonar.coverage.exclusions', [
                '**/config/**',
                '**/entity/**',
                '**/*Application.java',
                '**/*Exception.java',
                '**/*Request.java',
                '**/*Response.java'
        ].join(',')
    }
}

tasks.sonar {
    dependsOn ':core:core-api:jacocoMergedReport'
}